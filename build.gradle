plugins {
    id "com.github.node-gradle.node" version "2.2.3"
    id "nebula.ospackage" version "8.5.0"
    id 'com.palantir.git-version' version '0.12.3'
}

apply plugin: 'nebula.deb'

wrapper {
    jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

/*
 * Build OpenEMS Edge and Backend Components
 */
task buildComponents() {
    subprojects.each { proj ->
        if (proj.tasks.findAll { it.name == 'compileJava' }) {
            dependsOn(proj.compileJava)
        }
    }
}

/*
 * Build OpenEMS Edge fat-jar
 */
task buildEdge() {
    dependsOn ":io.openems.edge.application:export.EdgeApp"
    doLast {
        file("${buildDir}/openems-edge.jar").delete()
        copy {
            from file("io.openems.edge.application/generated/distributions/executable/EdgeApp.jar")
            into file("${buildDir}")
            rename ("EdgeApp.jar", "openems-edge.jar")
        }

    }
}

/*
 * Build OpenEMS Edge fat-jar into deb
 */
task buildEdgeDeb(dependsOn: buildEdge, type: Deb) {

    def details = versionDetails()
    def gitTag = details.lastTag
    println(gitTag.contains("release"))
    if (gitTag.contains("release") && gitTag.contains("/") && gitTag.split("/").length >= 3) {

        gitTag = gitTag.split("/")

        println("Releasing Oems Package ${gitTag[2]}")

        /* UPDATE VERSIONS HERE: */
        version = gitTag[2].split("-")[0]
        release = gitTag[2].split("-")[1]

        packageName = "openems-${gitTag[0]}"
        packageDescription = "Open EMS for Consolinno Leaflet"


    } else {
        println("No release tag found!")
        packageName = 'openems-dev'
        packageDescription = "DEV VERSION: Open EMS for Consolinno Leaflet"
    }
    requires('openjdk-8-jre')

    from("${buildDir}/openems-edge.jar") {
        into "/usr/lib/openems"
        /*rename ("openems-edge.jar", "openems.jar") */
    }

    from("openems.service") {
        into "/etc/systemd/system"
    }

    /* Install changelog */
    println("Adding changelog")
    from("debian/changelog.gz") {
        into "/usr/share/doc/${packageName}"

    }
    postInstall file('debian/postinst')

}

/*
 * Build OpenEMS Backend fat-jar
 */
task buildBackend() {
    dependsOn ":io.openems.backend.application:export.BackendApp"
    doLast {
        file("${buildDir}/openems-backend.jar").delete()
        copy {
            from file("io.openems.backend.application/generated/distributions/executable/BackendApp.jar")
            into file("${buildDir}")
            rename ("BackendApp.jar", "openems-backend.jar")
        }
    }
}

/*
 * Build OpenEMS UI for OpenEMS Backend
 */
task buildUiForBackend() {
    dependsOn ":ui:buildForBackend"
}

/*
 * Build OpenEMS UI for OpenEMS Edge
 */
task buildUiForEdge() {
    dependsOn ":ui:buildForEdge"
}

/*
 * Javadoc
 */
task buildAggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    destinationDir = file("$buildDir/www/javadoc")
    title = "OpenEMS Javadoc"
    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            options.addStringOption('Xdoclint:none', '-quiet')
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}

/*
 * Copies Bundle readme.adoc files to doc.
 */
task copyBundleReadmes() {
    doLast {
        // define target files
        def basePath = projectDir.getPath() + "/doc/modules/ROOT/pages/"
        def targetController = basePath + "edge/controller.adoc.d"
        def targetScheduler = basePath + "edge/scheduler.adoc.d"
        def targetNature = basePath + "edge/nature.adoc.d"
        def targetBridge = basePath + "edge/bridge.adoc.d"
        def targetDeviceService = basePath + "edge/device_service.adoc.d"

        // initialize target files and directories
        [targetController, targetScheduler, targetNature, targetBridge, targetDeviceService].each { target ->
            delete fileTree(dir: target, include: '**/*.adoc')
            new File(target + "/_include.adoc").write('')
        }

        subprojects.each { proj ->
            // in each subproject (= bundle)...
            proj.file(".").listFiles().each { sourceFile ->
                // find the 'readme.adoc' file
                if(sourceFile.getName().equalsIgnoreCase("readme.adoc")) {
                    def bundle = sourceFile.getParentFile().getName()
                    def target = null
                    // evaluate the OpenEMS Component ('Backend' or 'Edge')
                    if(bundle.startsWith("io.openems.edge.")) {
                        // evaluate the bundle type (e.g. 'Controller')
                        def edgeBundle = bundle.substring("io.openems.edge.".length())
                        if(edgeBundle.endsWith(".api")) {
                            target = targetNature
                        } else if(edgeBundle.startsWith("controller.")) {
                            target = targetController
                        } else if(edgeBundle.startsWith("scheduler.")) {
                            target = targetScheduler
                        } else if(edgeBundle.startsWith("bridge.")) {
                            target = targetBridge
                        } else {
                            target = targetDeviceService
                        }
                    } else if(bundle.startsWith("io.openems.wrapper")) {
                        // ignore
                        return
                    }

                    // could we exactly evaluate the bundle type and set the targetFile?
                    if(target == null) {
                        throw new Exception("Bundle type is unknown for " + bundle)
                    }

                    // copy the readme file to "target.d" folder
                    copy {
                        from (sourceFile.toPath())
                        into (target)
                        rename { fileName ->
                            return bundle + ".adoc"
                        }
                    }

                    // append content of source file to target file
                    File targetFile = new File(target + "/_include.adoc")
                    targetFile.append("\ninclude::" + bundle + ".adoc[leveloffset=+1]\n")
                }
            }
        }
    }
}

/*
 * Build Antora docs
 */
task buildAntoraDocs(dependsOn: copyBundleReadmes) {
    dependsOn ":doc:buildAntoraDocs"
    doLast {
        copy {
            from "doc/build/www"
            into "${buildDir}/www"
        }
    }
}